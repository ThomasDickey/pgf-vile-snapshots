dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Revision: 1.7 $)
### Use "configure -with-screen" to override the default configuration, which is
### termcap-based on unix systems.

dnl $Header: /usr/build/VCS/pgf-vile/RCS/configure.in,v 1.7 1994/09/23 04:21:19 pgf Exp $

dnl
AC_INIT(mktbls.c)

###	checks for alternative programs
AC_PROG_CC
AC_PROG_CPP
AC_GCC_TRADITIONAL
AC_PROG_INSTALL

###	use option --with-warnings to turn on all gcc warnings
AC_WITH(warnings, [with_warnings=$withval])
if test -n "$GCC"
then
	if test -z "$CFLAGS"
	then
CFLAGS="-O"
	fi
	if test -n "$with_warnings"
	then
CFLAGS="$CFLAGS -Wall -Wshadow -Wconversion -Wstrict-prototypes -Wmissing-prototypes"
	fi
fi

# X11 checks will sometimes add to CFLAGS.  We want the state of CFLAGS
# prior to these checks
saved_CFLAGS=$CFLAGS

###	checks for UNIX variants that set C preprocessor variables
AC_AIX
AC_ISC_POSIX
AC_MINIX
AC_DYNIX_SEQ
AC_IRIX_SUN

###	checks for header files
AC_STDC_HEADERS
AC_DIR_HEADER
AC_XENIX_DIR
AC_HAVE_HEADERS(unistd.h stdlib.h stddef.h string.h memory.h fcntl.h \
                poll.h termios.h termio.h sgtty.h \
		ioctl.h select.h setjmp.h signal.h)

###	checks for <sys/xxx> header files (separate, to avoid bug in autoconf)
AC_HAVE_HEADERS(sys/stat.h sys/filio.h sys/ioctl.h \
		sys/resource.h sys/wait.h sys/time.h sys/select.h)

###	checks for typedefs
AC_RETSIGTYPE
AC_DEFINE(SIGT,RETSIGTYPE)
case "$DEFS" in
	*RETSIGTYPE=void* ) AC_DEFINE(SIGRET,[/*do nothing*/]) ;;
	* ) AC_DEFINE(SIGRET,[return 0]) ;;
esac

VC_SIZE_T
AC_UID_T
AC_PID_T

###	checks for library functions
AC_SETVBUF_REVERSED

###	checks for structures
AC_STAT_MACROS_BROKEN
AC_ST_BLKSIZE
AC_ST_BLOCKS
AC_ST_RDEV
AC_TIME_WITH_SYS_TIME
AC_STRUCT_TM

###	checks for compiler characteristics
AC_CONST

# I haven't found a "losing" compiler to check the following on.  I gather
# that some losing compiler's can be found on pyramid's, aix, and Apple's AUX2.
AC_COMPILE_CHECK([to see if switch cases work with structure offsets], [],
[struct foo {int a,b;};
 extern getpid();
 switch(getpid()){case ((int) &(((struct foo *)0)->b)) : printf("foo"); } ],
[],[AC_DEFINE(HAVE_LOSING_SWITCH_WITH_STRUCTURE_OFFSET)])

VC_CPP_SUBS

###	checks for system services and user specified options
AC_LONG_FILE_NAMES
AC_RESTARTABLE_SYSCALLS
VC_HAVE_LIBRARY(nsl)		# needed on some systems to find gethostname

AC_SUBST(CFLAGS)
AC_SUBST(LIBS)
AC_SUBST(X_CFLAGS)
AC_SUBST(X_LIBS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(SCREEN)
AC_SUBST(TARGET)
dnl
dnl The killpg test and the tests for the presence of certain functions on
dnl a particular operating system are done prior to the checks for existence
dnl of screen libraries due to a problem with -lXt on certain SunOS systems.
dnl The nature of this problem is described in the FAQ on X.  Look for the
dnl following subject: ``What are these problems with "_XtInherit not found"
dnl on the Sun? ...''  The long and the short of it is that AC_HAVE_FUNCS will
dnl not work properly (without being rewritten) if placed after adding -lXt to
dnl LIBS on SunOS (and perhaps some others as well).
dnl
dnl Note: VC_KILLPG will call VC_SETPGRP
VC_KILLPG
AC_HAVE_FUNCS(sigprocmask setsid select poll strchr strrchr gethostname \
	getcwd getwd setvbuf access mkdir tcgetattr ttyname)

AC_DEFINE(UNIX)
AC_DEFINE(scrn_chosen)
# Try to eventually eliminate the following
AC_DEFINE(os_chosen)
AC_WITH(screen, [screen=$withval])
if test -z "$screen"; then
	AC_WITH(scr, [screen=$withval])
fi

if test -z "$screen"; then
	screen=termcap;
fi

case "$screen" in
changequote(,)dnl
	openlook | OpenLook | motif | Motif | [xX] | [xX][tT]* | [xX]11 )
changequote([,])dnl
		AC_FIND_XTRA
		# GNU autoconf-1.9 does not give me everything I need for
		# X_CFLAGS.  The following test will (hopefully) find the
		# additional flags needed to make it through X11/Xos.h.
		AC_CHECKING([for additional compiler flags needed with X11])
		if mkdir conftestdir; then
			cd conftestdir
			cat > Imakefile <<'EOF'
findstddefs:
	@echo 'im_std_defines="${STD_DEFINES}"'
EOF
			if (xmkmf) >/dev/null 2>/dev/null && test -f Makefile; then
				# GNU make sometimes prints "make[1]: Entering...", which
				# would confuse us.
				eval `make findstddefs 2>/dev/null | grep -v make`
				X_CFLAGS="$X_CFLAGS $im_std_defines"
			fi
			cd ..
			rm -rf conftestdir
		fi

		LDFLAGS="$LDFLAGS $X_LIBS"
		CFLAGS="$CFLAGS $X_CFLAGS"
		SCREEN=x11
		TARGET=xvile
		AC_DEFINE(X11)
		AC_DEFINE(XTOOLKIT)
		VC_HAVE_LIBRARY(X11)
		LIBS="-lXt $LIBS"
		AC_DEFINE(HAVE_LIBXT)
		AC_COMPILE_CHECK([-lXt], [], [XtAppInitialize();],[],
			AC_WARN(
[Unable to successfully link X Toolkit library (-lXt) with test program.
You will have to check and add the proper libraries by hand to makefile.]))
	;;
esac

case "$screen" in
	open* | Open* )
		AC_DEFINE(OL_WIDGETS)
		if test -n "$OPENWINHOME"; then
			X_LIBS="-L${OPENWINHOME}/lib"
			X_CFLAGS="$X_CFLAGS -I${OPENWINHOME}/include"
		fi
		LDFLAGS="$LDFLAGS $X_LIBS"
		VC_HAVE_LIBRARY(Xmu)
		VC_HAVE_LIBRARY(Xol,[LIBS="-lXol $LIBS"],
			AC_ERROR(
[Unable to successfully link OpenLook library (-lXol) with test program]))
	;;
	motif* | Motif* )
		AC_DEFINE(MOTIF_WIDGETS)
		VC_HAVE_LIBRARY(gen)
		VC_HAVE_LIBRARY(Xmu)
		VC_HAVE_LIBRARY(Xext)
		VC_HAVE_LIBRARY(Xm,[LIBS="-lXm $LIBS"],
			AC_ERROR(
[Unable to successfully link Motif library (-lXm) with test program]))
	;;
changequote(,)dnl
	x | X | [Xx][Tt]* | [Xx]11 )
changequote([,])dnl
		AC_DEFINE(NO_WIDGETS)
	;;
	* )
		SCREEN=tcap
		TARGET=vile
		AC_DEFINE(TERMCAP)
		AC_HAVE_LIBRARY(termlib, [LIBS="$LIBS -ltermlib" termlib=found])
		if test "$termlib" != found; then
			# allow curses library for broken AIX system.
			AC_HAVE_LIBRARY(curses, [LIBS="$LIBS -lcurses" termlib=found])
			AC_HAVE_LIBRARY(termcap, [LIBS="$LIBS -ltermcap" termlib=found])
		fi
		if test "$termlib" != found; then
			AC_HAVE_LIBRARY(termcap, [LIBS="$LIBS -ltermcap" termlib=found])
		fi
		if test "$termlib" != found; then
			AC_WARN([Can't find -ltermlib, -lcurses, or -ltermcap])
		fi
	;;
esac

###	mandatory checks for missing headers
VC_MISSING_EXTERN( \
getenv \
getwd \
malloc \
realloc \
rindex \
strtol \
)

###	optional checks for missing headers (enabled with --with-warnings)
if test -n "$with_warnings"
then
VC_MISSING_EXTERN( \
_filbuf \
_flsbuf \
access \
alarm \
atoi \
bzero \
chdir \
close \
dup \
execlp \
fclose \
fflush \
fgetc \
fileno \
fork \
fprintf \
fputc \
fputs \
fread \
free \
fseek \
fwrite \
gethostname \
getpgrp \
getpid \
getuid \
ioctl \
isatty \
kill \
killpg \
longjmp \
memset \
mkdir \
open \
perror \
pipe \
printf \
puts \
qsort \
read \
select \
setbuf \
setbuffer \
setjmp \
setpgrp \
setsid \
setvbuf \
sleep \
sscanf \
system \
time \
unlink \
wait \
write \
)
fi

###	output makefile and config.h
CFLAGS=$saved_CFLAGS
changequote({,})dnl
AC_OUTPUT(makefile config_h,
{
echo creating config.h
sed	-e '/^# /d' \
	-e 's/ -D/\
#define /g' \
	-e 's/\(#define [A-Za-z_][A-Za-z0-9_]*\)=/\1	/g' \
	config_h >config.h
}
echo removing config_h
rm config_h
)
changequote([,])dnl
